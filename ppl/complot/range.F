      SUBROUTINE RANGE(UAMIN,UAMAX,NAINTS,UMINR,UMAXR,DINT)
C** 
C**    @(#)range.f	1.1    3/10/88
C**
C**
C***********************************************************************
C**
C**                 PLOT+ Scientific Graphics System
C**
C***********************************************************************
C**
C**
C
C     CALL RANGE(UMIN,UMAX,NINTS,UMINR,UMAXR,DINT)
C
C     THIS SUBROUTINE FINDS TWO VALUES FOR A VARIABLE SUCH THAT
C     THE RANGE BETWEEN THEM IS EVENLY DIVISIBLE INTO APPROXI-
C     MATELY A SPECIFIED NUMBER OF INTERVALS AND CONTAINS THE
C     MINIMUM AND MAXIMUM VALUES OF THE VARIABLE.
C
C     THE VARIOUS PARAMETERS AND THEIR DEFINITIONS ARE LISTED
C     BELOW:
C        UMIN   - MINIMUM VALUE OF THE VARIABLE.
C        UMAX   - MAXIMUM VALUE OF THE VARIABLE.
C        NINTS  - APPROXIMATE NUMBER OF INTERVALS INTO WHICH THE
C                 RANGE IS TO BE DIVIDED.
C        UMINR  - MINIMUM VALUE OF THE RANGE.
C        UMAXR  - MAXIMUM VALUE OF THE RANGE.
C        DINT   - INTERVAL SIZE.
C
C
C     TRANSFER THE PARAMETERS TO NEW VARIABLES.
C
      UMIN = UAMIN
      UMAX = UAMAX
      NINTS = IABS(NAINTS)
C
C     CHECK THE PARAMETERS TO MAKE SURE THAT THEY ARE VALID.
C
      IF(UMIN .GT. UMAX)THEN
        TEMP = UMIN
        UMIN = UMAX
        UMAX = TEMP
      ENDIF
      IF(NINTS .EQ. 0)NINTS = 1
C
C     FIND THE APPROXIMATE SIZE OF THE INTERVAL.
C
      TEMP = (UMAX - UMIN) / FLOAT(NINTS)
      IF(TEMP .EQ. 0.0)TEMP = UMAX
C
C     SCALE THE INTERVAL SIZE BY POWERS OF TEN TO A VALUE BETWEEN
C     ONE AND TEN.
C
      NT = IFIX(ALOG10(TEMP))
      IF(TEMP .LT. 1.0)NT = NT - 1
      POW = 10.0 ** NT
      TEMP = TEMP / POW
C
C     FIND THE CLOSEST PERMISSIBLE VALUE FOR THE INTERVAL SIZE.
C
      IF(TEMP .LT. SQRT(2.0))THEN
        DINT = POW
      ELSE IF(TEMP .LT. SQRT(10.0))THEN
        DINT = 2.0 * POW
      ELSE IF(TEMP .LT. SQRT(50.0))THEN
        DINT = 5.0 * POW
      ELSE
        DINT = 10.0 * POW
      ENDIF
C
C     CALCULATE THE MINIMUM VALUE OF THE RANGE.
C
      FM = UMIN / DINT
      M = IFIX(FM)
      IF(FM .LT. 0.0)M = M - 1
      UMINR = DINT * FLOAT(M)
C
C     CALCULATE THE MAXIMUM VALUE OF THE RANGE.
C
      FM = UMAX / DINT
      M = IFIX(FM)
      IF(FM .GT. 0.0)M = M + 1
      UMAXR = DINT * FLOAT(M)
      RETURN
C
      END
