	SUBROUTINE VECFLD(U,V,NXS,NYS,PX,PY,XF,YF)
	real u,v,px,py,xf,yf
	integer nxs,nys
C** 
C**    @(#)vecfld.F	1.5    9/21/89
C**
C**
C***********************************************************************
C**
C**                 PLOT+ Scientific Graphics System
C**
C***********************************************************************
C**
C**
C
C	ARRAY X HOLD 2 GRIDS, U(X) IN THE FIRST
C	NX BY NY GRID AND V(Y) IN THE SECOND
C
	DIMENSION U(NXS,NYS),V(NXS,NYS),PX(NXS),PY(NYS)
C
C
#ifdef unix
	INCLUDE 'pplinc/PARAMPL5.DAT'
        INCLUDE 'pplinc/LINES.INC'
	INCLUDE 'pplinc/PEN.INC'
	INCLUDE 'pplinc/CMRD.INC'
	INCLUDE 'pplinc/MISS.INC'
	INCLUDE 'cmdinc/SWITCH.INC'
	INCLUDE 'pplinc/HD.INC'
	INCLUDE 'pplinc/AXIS.INC'
	INCLUDE 'pplinc/LABCOM.INC'
	INCLUDE 'pplinc/VECTOR.INC'
#else
        INCLUDE 'PPLINC:PARAMPL5.DAT'
        INCLUDE 'PPLINC:LINES.INC'
        INCLUDE 'PPLINC:PEN.INC'
        INCLUDE 'PPLINC:CMRD.INC'
        INCLUDE 'PPLINC:MISS.INC'
        INCLUDE 'CMDINC:SWITCH.INC'
        INCLUDE 'PPLINC:HD.INC'
        INCLUDE 'PPLINC:AXIS.INC'
        INCLUDE 'PPLINC:LABCOM.INC'
        INCLUDE 'PPLINC:VECTOR.INC'
#endif
	real xfuser,yfuser,xlousr,ylousr,zave,xt,yt
	real xx2,yy2,zz,vfact,xtm,ytm,xtx,ytx,xx1,yy1,vx,vy,vclen
	real thfact,xn,yn,xl,yl,dy,dx
	integer ic,i,j,lnblk,iln
	CHARACTER VECLAB*20
C
C	VECTOR,X,Y,VFACT,LABEL
C
	    IF(VUSER)THEN
	    	XFUSER=1.
		YFUSER=1.
		YLOUSR=0.
		XLOUSR=0.
	    ELSE
	    	XFUSER=XF
		YFUSER=YF
		XLOUSR=XLO
		YLOUSR=YLO
	    ENDIF
	XT=VXLOC/XFUSER+XLOUSR
	YT=VYLOC/YFUSER+YLOUSR
	IF(VAUTO)THEN
c
c	use average vector length for scale if auto
c
	    IC=0
	    ZAVE=0.0
	    DO 10 I=1,NX,VSKPX
	    DO 10 J=1,NY,VSKPY
	    XX2=U(I,J)
	    YY2=V(I,J)
C
C	TEST FOR MISSING DATA
C
	    IF((ZLE.AND.XX2.LE.CMZLE).OR.
     *	       (ZEQ.AND.XX2.EQ.CMZEQ).OR.
     *	       (ZGE.AND.XX2.GE.CMZGE).OR.
     *	       (ZLE.AND.YY2.LE.CMZLE).OR.
     *	       (ZEQ.AND.YY2.EQ.CMZEQ).OR.
     *	       (ZGE.AND.YY2.GE.CMZGE))GOTO 10
	    ZZ=SQRT(XX2**2+YY2**2)
	    ZAVE=ZAVE+ZZ
	    IC=IC+1
10	    CONTINUE
	    IF(IC.EQ.0)IC=1
	    ZAVE=ZAVE/FLOAT(IC)
	    VFACT=VLEN/(2.0*ZAVE)
	ELSE
	    VFACT=VLEN/VUSRLN
	ENDIF
	CALL COLOR(IPEN(1))
	call trans(0,xmin,ymin,xtm,ytm)
	call trans(0,xmax,ymax,xtx,ytx)
	DX=(xtx-xtm)/(NX-1)
	DY=(ytx-ytm)/(NY-1)
	DO 100 I=1,NX,VSKPX
	DO 100 J=1,NY,VSKPY
	XX2=U(I,J)
	YY2=V(I,J)
C
C	TEST FOR MISSING DATA
C
	IF((ZLE.AND.XX2.LE.CMZLE).OR.
     *	   (ZEQ.AND.XX2.EQ.CMZEQ).OR.
     *	   (ZGE.AND.XX2.GE.CMZGE).OR.
     *	   (ZLE.AND.YY2.LE.CMZLE).OR.
     *	   (ZEQ.AND.YY2.EQ.CMZEQ).OR.
     *	   (ZGE.AND.YY2.GE.CMZGE))GOTO 100
C
C	TAIL LOCATION USER UNITS
C
	IF(JTYPE.EQ.-1)THEN
	    XX1 = xtm + DX*(I-1)
	    YY1 = ytm + DY*(J-1)
	ELSE
	    call trans(0,px(i),py(j),xx1,yy1)
	ENDIF
C
C	HEAD LOCATION USER UNITS
C
	XX2 = XX1 + XX2*VFACT/XF
	YY2 = YY1 + YY2*VFACT/YF
C
C	CALC REAL LENGTH
C
	VX=(XX2-XX1)*XF
	VY=(YY2-YY1)*YF
	VCLEN=SQRT(VX**2+VY**2)
C
C	IF VECTOR IS TOO SMALL PLOT A POINT
C
	IF(VCLEN.LT.VMINLN)THEN
	    CALL PLOT(XX1,YY1,0,0)
	    CALL PLOT(XX1,YY1,1,0)
	    GOTO 100
	ENDIF
C
C	SET UP THE ARROW FACTOR HEAD LENGTH SCALE
C
	IF(VCLEN.GT.ARMAX)THEN
	    THFACT=ARMAX*HFACT/VCLEN
	ELSE IF(VCLEN.LT.ARMIN)THEN
	    THFACT=ARMIN*HFACT/VCLEN
	ELSE
	    THFACT=HFACT
	ENDIF
C
C	DRAW ARROW HEADS
C
	XN=XX2+(-VX-.35*VY)*THFACT/XF
	YN=YY2+(-VY+.35*VX)*THFACT/YF
	CALL PLOT(XN,YN,0,0)
	CALL PLOT(XX2,YY2,1,0)
C
	XN=XX2+(-VX+.35*VY)*THFACT/XF
	YN=YY2+(-VY-.35*VX)*THFACT/YF
	CALL PLOT(XN,YN,1,0)
	CALL PLOT(XX2,YY2,1,0)
C
	CALL PLOT(XX1,YY1,1,0)
100	CONTINUE
C
C	DRAW SCALE  MAKE SCALE 1 INCH LONG AT XT,YT
C
	IF(.NOT.VKEY)THEN
	    CALL COLOR(IPEN(0))
	    RETURN
	ENDIF
	CALL WINDOW(0.,0.,0.,0.)
	XX1=XT
	YY1=YT
	XX2=XT+VLEN/XF
	YY2=YT
C
C	DRAW ARROW HEADS
C
	IF(VLEN.GT.ARMAX)THEN
	    THFACT=ARMAX*HFACT/VLEN
	ELSE IF(VLEN.LT.ARMIN)THEN
	    THFACT=ARMIN*HFACT/VLEN
	ELSE
	    THFACT=HFACT
	ENDIF
	XN=XX2-THFACT*VLEN/XF
	YN=YY2+.35*THFACT*VLEN/YF
	CALL PLOT(XN,YN,0,0)
	CALL PLOT(XX2,YY2,1,0)
C
	XN=XX2-THFACT*VLEN/XF
	YN=YY2-.35*THFACT*VLEN/YF
	CALL PLOT(XN,YN,1,0)
	CALL PLOT(XX2,YY2,1,0)
C
	CALL PLOT(XX1,YY1,1,0)
C
C	DRAW LABEL
C
	CALL COLOR(IPEN(0))
	WRITE (VECLAB,VFRMT) VLEN/VFACT
	ILN=LNBLK(VECLAB,20)
	XL = XX2 + .125/XF
	YL = YY2 - HLABS*0.5/YF
	CALL SYMBEL(XL,YL,0.,HLABS,ILN,VECLAB)
	IF(IWIND.GT.0)CALL WINDOW(XLO,YLO,XHI,YHI)
	RETURN
	END
