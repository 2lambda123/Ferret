#!/bin/csh -f
# script to check metafiles (.plt*, .gif* and .ps) created after a run of the 
# benchmark routine against master metafiles. 
# kob  10/97
# acm   4/05 add more .plt files for checking, new directory metafile_masters_v580

# set machine as at the start of run_all

# corona has HOSTTYPE=sun, dread has HOSTTYPE=solaris
if ( $HOSTTYPE =~ *sun* ) then
   if ( `uname -r` =~ *5*  ) then
      set machine = "sol"
   else
      set machine = "sun"
   endif
else if ( $HOSTTYPE =~ *solaris* ) then
   set machine = "sol" 
else if ( $HOSTTYPE =~ *alpha* ) then
   set machine = "alp" 
else if ( $HOSTTYPE =~ *aix* ) then
   set machine = "aix" 
else if ( $HOSTTYPE =~ *iris* ) then
   set machine = "sgi"
else if ( $HOSTTYPE =~ *hp* ) then
   set machine = "hp"
else if ( $HOSTTYPE =~ *ia64-linux*) then
    set machine = "ia64-linux"
else if ( $HOSTTYPE =~ *x86_64-linux*) then
    set machine = "x86_64-linux"
else if ( $HOSTTYPE =~ *linux*) then
    set machine = "linux"
else
   set machine = "dec"
endif

if ($machine == "ia64-linux") then
   set plt_files = ( bn*.plt dots.plt flowlines.plt graticules.plt multi_line_labels.plt \
   pattern*.plt polygon*.plt vec_curv*.plt)
   set gif_files = ( bn*.gif )
   set ps_files = (bn*.ps)
   set num_plt_files = `ls -l *.plt | wc -l `
   
   set err_plt = 0
   foreach plt_file ($plt_files)
    set size = `ls -l $plt_file | cut -b 21-27`
    set size2 = `ls -l metafile_masters_v580/$plt_file | cut -b 21-27`
    if ($size != $size2) set err_plt = 1
    if ($num_plt_files != 14 ) set err_plt = 1
   end
   
   set err_gif = 0
   foreach gif_file ($gif_files)
    set size = `ls -l $gif_file | cut -b 21-26`
    set size2 = `ls -l metafile_masters_v580/$gif_file | cut -b 21-26`
    if ($size != $size2) set err_gif = 1
   end
    
   set err_ps = 0
   foreach ps_file ($ps_files)
    set size = `ls -l $ps_file | cut -b 21-27`
    set size2 = `ls -l metafile_masters_v580/$ps_file | cut -b 21-27`
    if ($size != $size2) set err_ps = 1
   end

else

   set plt_files = ( bn*.plt dots.plt flowlines.plt graticules.plt multi_line_labels.plt \
   pattern*.plt polygon*.plt vec_curv*.plt)
   set gif_files = ( bn*.gif )
   set ps_files = (bn*.ps)
   set num_plt_files = `ls -l *.plt | wc -l `
   
   set err_plt = 0
   foreach plt_file ($plt_files)
    set size = `ls -l $plt_file | cut -b 26-32`
    set size2 = `ls -l metafile_masters_v580/$plt_file | cut -b 26-32`
    if ($size != $size2) set err_plt = 1
    if ($num_plt_files != 14 ) set err_plt = 1
   end
   
   set err_gif = 0
   foreach gif_file ($gif_files)
    set size = `ls -l $gif_file | cut -b 26-32`
    set size2 = `ls -l metafile_masters_v580/$gif_file | cut -b 26-32`
    if ($size != $size2) set err_gif = 1
   end
 
   set err_ps = 0
   foreach ps_file ($ps_files)
    set size = `ls -l $ps_file | cut -b 26-32`
    set size2 = `ls -l metafile_masters_v580/$ps_file | cut -b 26-32`
    if ($size != $size2) set err_ps = 1
   end
   
endif

if ($err_plt) then
	echo "CHECK PLT FILES - THERE HAS BEEN AN ERROR"
else
	echo "    PLT files look good....."
endif 
if ($err_gif) then
	echo "CHECK GIF FILES - THERE HAS BEEN AN ERROR"
else
	echo "    GIF files look good....."
endif
if ($err_ps) then
	echo "CHECK PS FILES - THERE HAS BEEN AN ERROR"
else
	echo "    PS files look good......"
endif

