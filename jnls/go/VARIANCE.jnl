\CANCEL MODE VERIFY
! updated 10/93 for Ferret V3.01
! Description:  define FERRET variables for covariance and correlation

SAY ... Variance and Covariance: Instructions:
SAY Use the LET command to define the variable(s) P (and Q) as
SAY your variable(s) of interest (e.g. yes? LET P = u[x=180,y=0])
SAY The variance of P will be variable P_VAR  (Q --> Q_VAR)
SAY The covariance will be COVAR The correlation will be CORREL.
SAY Type GO TVAR_N to obtain n/n+1 statistical correction factor
SAY ...

! deviations, squared deviations, cross deviations
let P_AVE = P[L=@ave]
let Q_AVE = Q[L=@ave]
let P_DEV = P - P_AVE
let Q_DEV = Q - Q_AVE
let P_DSQ = P_DEV * P_DEV
let Q_DSQ = Q_DEV * Q_DEV
let PQ_DSQ= P_DEV * Q_DEV

! variances, covariances
let/title="VARIANCE OF P" 	   P_VAR = P_DSQ[l=@ave]
let/title="VARIANCE OF Q" 	   Q_VAR = Q_DSQ[l=@ave]
let/title="COVARIANCE OF P AND Q"  COVAR = PQ_DSQ[l=@ave]
let/title="CORRELATION OF P AND Q" CORREL= COVAR / (P_VAR*Q_VAR)^.5

SET MODE/LAST VERIFY
