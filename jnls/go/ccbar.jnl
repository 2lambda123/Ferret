\CANCEL MODE verify

! Continuous colorbar script.   
! uses a shade plot inside a viewport to make a colorbar
! 5/16/02  ms and acm

! Usage:

!    GO ccbar xloc yloc xlen ylen v1 v2 dv orient
 
! where the first four arguments are in inches:
!         xloc = x-coordinate of lower left corner of clorbar 
!         yloc = y-coordinate of lower left corner of clorbar 
!         xlen = x length of colorbar    
!         ylen = y length of colorbar
!         v1 = lo value on colorbar 
!         v2 = hi value on colorbar
!         dv = delta value for colorbar labels
!         orientation = v for vertical or h for horizontal, default v
!         palette to use, optional

! Check inputs. First 7 are required

QUERY/IGNORE $1%<Usage: GO ccbar xloc yloc xaxl yaxl v1 v2 dv orient"%
QUERY/IGNORE $2%<Usage: GO ccbar xloc yloc xaxl yaxl v1 v2 dv orient"%
QUERY/IGNORE $3%<Usage: GO ccbar xloc yloc xaxl yaxl v1 v2 dv orient"%
QUERY/IGNORE $4%<Usage: GO ccbar xloc yloc xaxl yaxl v1 v2 dv orient"%
QUERY/IGNORE $5%<Usage: GO ccbar xloc yloc xaxl yaxl v1 v2 dv orient"%
QUERY/IGNORE $6%<Usage: GO ccbar xloc yloc xaxl yaxl v1 v2 dv orient"%
QUERY/IGNORE $7%<Usage: GO ccbar xloc yloc xaxl yaxl v1 v2 dv orient"%
QUERY/IGNORE $8%v%
LET ccb_orient = $8"1|v>1|h>2|*>1"

DEFINE SYM ccb_pal = /pal=$9"default"

! Define the viewport to contain the colorbar

LET ccb_x1 = $1/($ppl$width)
LET ccb_y1 = $2/($ppl$height)
LET ccb_x2 = ($1+$3)/($ppl$width)
LET ccb_y2 = ($2+$4)/($ppl$height)

DEFINE VIEW/AXES/TEXT=1/XLIMITS=`ccb_x1`,`ccb_x2`/YLIMITS=`ccb_y1`,`ccb_y2` ccb_cbar
SET VIEW ccb_cbar

!  Set up to plot a variable with desired range
LET ccb_v1 = $5
LET ccb_v2 = $6
LET ccb_dv = $7

DEFINE AXIS/X=1:2:1 ccb_xcb 
DEFINE AXIS/Y=`ccb_v1`:`ccb_v2`:`ccb_dv` ccb_ycb
LET ccb_var = 0*x[gx=ccb_xcb] + y[gy=ccb_ycb]

! ----------------------------------------------------------------------
!  Set the label format.  This could be further customized.

LET ccb_fmt = 0
IF `ccb_v1 GE -99` THEN
  LET ccb_fmt = 1
  IF `ccb_v2 LT 1000` THEN DEFINE SYM ccb_yform = (I3)
ENDIF

IF `ccb_v1 GE -9` THEN
  LET ccb_fmt = 1
  IF `ccb_v2 LT 100` THEN DEFINE SYM ccb_yform = (I2)
ENDIF

IF `ccb_v1 GE 0` THEN 
  LET ccb_fmt = 1
  IF `ccb_v2 LT 10` THEN DEFINE SYM ccb_yform = (I1)
ENDIF

IF `ccb_v2 - ccb_v1 LT 2` THEN 
  LET ccb_fmt = 0
ENDIF
! ----------------------------------------------------------------------

!  Plot the variable within the colorbar.

IF `ccb_orient EQ 1` THEN
   SHADE/LEV=(`ccb_v1`,`ccb_v2+ccb_dv`,`ccb_dv`)/NOKEY/NOLAB/AXES=0,0,0,1($ccb_pal)/SET ccb_var
   IF `ccb_fmt eq 1` THEN PPL YFOR ($ccb_yform)
   PPL SHADE
ELSE
   SHADE/TRANS/LEV=(`ccb_v1`,`ccb_v2+ccb_dv`,`ccb_dv`)/NOKEY/NOLAB/AXES=1,0,0,0($ccb_pal)/SET ccb_var
   IF `ccb_fmt eq 1` THEN PPL XFOR ($ccb_yform)
   PPL SHADE
ENDIF

! Clean up
CANCEL VAR ccb_*
CANCEL SYM ccb_*
CANCEL AXIS ccb_xcb
CANCEL AXIS ccb_ycb

SET MODE/LAST verify
