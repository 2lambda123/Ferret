\cancel mode verify
! vertical_section.jnl
! 12/1/94 *sh*

! Description: Create an arbitrary 2D vertical section from a 3D field

! usage:
!                             arg1   arg2   a3  a4  a5  a6  a7 a8
!	GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz

! arguments
! variable (arg1) - the variable to be sampled.  >>BE SURE TO SPECIFY A
!			UNIQUE T= OR L= VALUE IN THE DEFINITION<<
! zaxis    (arg2) - the variable defining the points of the vertical axis
!			Typically this is Z[g=var], "var" is the arg1 variable
! xlo,ylo,xhi,yhi - the lower left and upper right coordinates of the section
!			to be extracted.
!			>> NOTE THAT COORDINATES MUST BE PURELY NUMERICAL:
!			FOR LONGITUDE DEGREES EAST USE NUMERICAL LONGITUDE
!			FOR LONGITUDE DEGREES WEST USE 360-LONGITUDE
!			FOR LATITUDE DEGREES SOUTH USE (-1) * LATITUDE
!			FOR LATITUDE DEGREES NORTH USE NUMERICAL LATITUDE
! nx	   (arg7) - the number of desired horizontal points
! nz	   (arg8) - the number of desired vertical   points

! example:
!   Create a section of the Levitus climatological temperature running
!   from (160e,20s) to (140w,30n)

!	yes? SET DATA levitus_climatology
!	yes? GO vertical_section "temp[x=150e:130w,y=25s:35n]" z[g=temp] 160,-20,220,30 61,15
!	yes? FILL vsect[d=vsection.dat]
!  Plot the section line, too
!	yes? SET WINDOW/NEW
!	yes? SHADE/Z=0 temp[d=levitus_climatology]
!	yes? PLOT/VS/OVER/SYM x_vsect[i=1:61], y_vsect[i=1:61]

! Notes:
!   1) Quotation marks avoid a syntax error from the comma in "temp[...,...]"
!   2) The region x=150e:130w,y=25s:35n is a subset of the full data
!		set chosen (arbitrarily) large enough to encompass the
!		section to be sampled.
!   3) The data set vsection.dat is created by "GO vertical_section ..."

! check the arguments
query/ignore $1%<Use: GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz%
query/ignore $2%<Use: GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz%
query/ignore $3%<Use: GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz%
query/ignore $4%<Use: GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz%
query/ignore $5%<Use: GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz%
query/ignore $6%<Use: GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz%
query/ignore $7%<Use: GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz%
query/ignore $8%<Use: GO vertical_section variable zaxis xlo,ylo,xhi,yhi  nx,nz%


! pre-load the data to make sure it was specified correctly
LET var_vsect = $1
LOAD var_vsect

! define the X,Y, and Z coordinates for sampling the 3D field
LET/QUIET x_vsect = ($5-($3))/($7-1) * (I-1) + ($3)
LET/QUIET y_vsect = ($6-($4))/($7-1) * (I-1) + ($4)
LET/QUIET z_vsect = $2

LET/QUIET horizontal_shape = 0 * x_vsect[i=1:$7]
LET/QUIET vertical_shape   = 0 * z_vsect[k=1:$8]

LET/QUIET xtup_vsec = x_vsect[i=1:$7] + vertical_shape
LET/QUIET ytup_vsec = y_vsect[i=1:$7] + vertical_shape
LET/QUIET ztup_vsec = z_vsect[k=1:$8] + horizontal_shape
LET/QUIET ttup_vsec = 1/0				! a place-holder

! sample the 3D field
cancel data/noerror vsection.dat
sp rm -f vsection.dat
user/command=sample/opt1=STANDARD_BAD/opt2=c/file=vsection.dat var_vsect,xtup_vsec,ytup_vsec,ztup_vsec,ttup_vsec

! read the sampled data back on a 2D grid
define axis/x=1:$7:1 xax
define axis/from_data/z/name=zax/depth z_vsect[k=1:$8]
define grid/x=xax/z=zax gsampled
file/var=vsect/grid=gsampled vsection.dat
set variable/title="Vertical Section" vsect

! the sampled data is ready to use
message/cont "To view section >>> FILL vsect[d=vsection.dat] <<<"

set mode/last verify




