\cancel mode verify
! label_hi_lo
! *8/01 *acm* 
! *1/02 *acm* range not gridpts for args 2 and 3 to find* functions
!             result is listed on X axis, with j=1 xpts,j=2 ypts,j=3 values

! Description: overlay labels for local extrema with H and L or the
!              values at those extrema
!
!   usage:
!	GO label_hi_lo var marktype xrng yrng hicolor locolor
!   where:
!	var	   Variable in the underlying plot. note do not use square
!                  bracket syntax (GO label_lo_hi sst[l=1]... will fail )
!
!	marktype   "mark" or "label": 
!			mark puts L and H
!			label puts the data value in a box at extremum
!
!	xrng     range in data units, in x and y direction
!	yrng       in which to look for local extrema
!
!	hicolor    color of box around HI labels, or of letter H, default black
!	locolor    color of box around LO labels, or of letter L, default black

! example:
!	yes? USE coads_climatology
!	yes? SET REGION/L=7/X=100:360/Y=-20:60
!	yes? FILL/LINE/PAL=no_red slp
!	yes? GO fland
!	yes? GO label_hi_lo.jnl slp label 5 4 green purple
!       yes? PAUSE
!	yes? CONTOUR slp
!	yes? GO fland
!	yes? GO label_hi_lo.jnl slp mark 3 3 red blue

! check arguments
QUERY/IGNORE $1%<Usage: GO label_hi_lo  var marktype xrng yrng xmin xmax ymin%
QUERY/IGNORE $2%<Usage: GO label_hi_lo  var marktype xrng yrng xmin xmax ymin%
QUERY/IGNORE $3%<Usage: GO label_hi_lo  var marktype xrng yrng xmin xmax ymin%
QUERY/IGNORE $4%<Usage: GO label_hi_lo  var marktype xrng yrng xmin xmax ymin%

QUERY/IGNORE $5%black|black|red|green|blue|lightblue|purple|*>black|%
QUERY/IGNORE $6%black|black|red|green|blue|lightblue|purple|*>black|%

! save region and grid information.  X and Y regions are cancelled below

DEFINE REGION/DEFAULT save

LET/QUIET xmax = ($ppl$xmax)
LET/QUIET xmin = ($ppl$xmin)
LET/QUIET ymax = ($ppl$ymax)
LET/QUIET ymin = ($ppl$ymin)

! compute local mins and maxes
LET/QUIET datlo = findlo($1[x=`xmin`:`xmax`,y=`ymin`:`ymax`], $3, $4)
LET/QUIET dathi = findhi($1[x=`xmin`:`xmax`,y=`ymin`:`ymax`], $3, $4)

LET/QUIET xph = dathi[j=1]
LET/QUIET yph = dathi[j=2]
LET/QUIET zph = dathi[j=3]

LET/QUIET xpl = datlo[j=1]
LET/QUIET ypl = datlo[j=2]
LET/QUIET zpl = datlo[j=3]

LOAD xph; LOAD yph; LOAD zph
LOAD xpl; LOAD ypl; LOAD zpl
let/quiet nhi = xph[x=@ngd]
let/quiet nlo = xpl[x=@ngd]

! I and J indices are used as indices of the extrema, so cancel the
! region that came in with the variable.

CAN REGION/X/Y

IF $2"|label>1|*>0|" THEN
! Mark the max's and mins with a rectangle, and the value of the extremum.
! Allow either violet or purple to be specified.   

  GO polymark POLY/OVER/NOLABEL/PAL=white/LINE/COLOR=$5%black|black|red|green|blue|lightblue|purple|violet>purple|*>black% xph,yph,zph,rectangle,1.8
  REPEAT/K=1:`nhi` (LET/QUIET xp=xph[i=`k`]; LET/QUIET yp=yph[i=`k`]; LET/QUIET zp=zph[i=`k`]; label `xp`, `yp-0.8`, 0,0, 0.09, "`zp`")

  GO polymark POLY/OVER/NOLABEL/PAL=white/LINE/COLOR=$6%black|black|red|green|blue|lightblue|purple|violet>purple|*>black% xpl,ypl,zpl,rectangle,1.8
  REPEAT/K=1:`nlo` (LET/QUIET xp=xpl[i=`k`]; LET/QUIET yp=ypl[i=`k`]; LET/QUIET zp=zpl[i=`k`]; label `xp`, `yp-0.8`, 0,0, 0.09, "`zp`")
ENDIF

IF $2"|mark>1|*>0|" THEN
! Mark the extrema with L and H 
! Allow either violet or purple to be specified.   

  GO polymark POLY/OVER/NOLABEL/PAL=$5%black|black|red|green|blue|lightblue|violet|purple>violet|*>black%/fill xph,yph,,hletter,1.2
  GO polymark POLY/OVER/NOLABEL/PAL=$6%black|black|red|green|blue|lightblue|violet|purple>violet|*>black%/fill xpl,ypl,zpl,lletter,1.2
ENDIF

! Restore input region
SET REGION save

SET MODE/LAST VERIFY
