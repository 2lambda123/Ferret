\cancel mode verify
! label_hi_lo
! *8/01 *acm* 

! Description: overlay labels for local extrema with H and L or the
!              values at those extrema
! NOTE:        Please use SET REGION to specify the region before 
!              calling this script.  
!
!   usage:
!	GO label_hi_lo var marktype xneigh yneigh hicolor locolor
!   where:
!	var	   Variable in the underlying plot
!	marktype   "mark" or "label": 
!			mark puts L and H
!			label puts the data value in a box at extremum
!	xneigh     argument to findhi,findlo: neighborhood in x direction
!	yneigh     argument to findhi,findlo: neighborhood in y direction
!	hicolor    color of box around hi labels, or of letter H, default black
!	locolor    color of box around lo labels, or of letter L, default black

! example:
!	yes? USE coads_climatology
!	yes? SET REGION/L=7/X=100:360/Y=-20:60
!	yes? FILL/LINE/PAL=no_red slp
!	yes? GO fland
!	yes? GO label_hi_lo.jnl slp label 5 4 green purple
!       yes? PAUSE
!	yes? CONTOUR slp
!	yes? GO fland
!	yes? GO label_hi_lo.jnl slp mark 3 3 red blue

! check arguments
QUERY/IGNORE $1%<Usage: GO label_hi_lo  var marktype xneigh yneigh xmin xmax ymin%
QUERY/IGNORE $2%<Usage: GO label_hi_lo  var marktype xneigh yneigh xmin xmax ymin%
QUERY/IGNORE $3%<Usage: GO label_hi_lo  var marktype xneigh yneigh xmin xmax ymin%
QUERY/IGNORE $4%<Usage: GO label_hi_lo  var marktype xneigh yneigh xmin xmax ymin%

QUERY/IGNORE $5%black|black|red|green|blue|lightblue|purple|*>black|%
QUERY/IGNORE $6%black|black|red|green|blue|lightblue|purple|*>black|%

! save region and grid information.  X and Y regions are cancelled below

DEFINE REGION/DEFAULT save

LET xmax = ($ppl$xmax)
LET xmin = ($ppl$xmin)
LET ymax = ($ppl$ymax)
LET ymin = ($ppl$ymin)

! compute local mins and maxes
LET/QUIET datlo = findlo($1[x=`xmin`:`xmax`,y=`ymin`:`ymax`], $3, $4)
LET/QUIET dathi = findhi($1[x=`xmin`:`xmax`,y=`ymin`:`ymax`], $3, $4)

LET/QUIET xph = dathi[i=1]
LET/QUIET yph = dathi[i=2]
LET/QUIET zph = dathi[i=3]

LET/QUIET xpl = datlo[i=1]
LET/QUIET ypl = datlo[i=2]
LET/QUIET zpl = datlo[i=3]

LOAD xph; LOAD yph; LOAD zph
LOAD xpl; LOAD ypl; LOAD zpl

! I and J indices are used as indices of the extrema, so cancel the
! region that came in with the variable.

CAN REGION/X/Y

IF $2"|label>1|*>0|" THEN
! Mark the max's and mins with a rectangle, and the value of the extremum.
! Allow either violet or purple to be specified.   

  go polymark poly/over/nolabel/pal=white/line/color=$5%black|black|red|green|blue|lightblue|purple|violet>purple|*>black% xph,yph,zph,rectangle,1.8
  repeat/k=1:20 (let/quiet xp=xph[j=`k`]; let/quiet yp=yph[j=`k`]; let/quiet zp=zph[j=`k`]; IF `xp NE 999` THEN IF `yp ne 999` THEN  label `xp`, `yp-0.8`, 0,0, 0.09, "`zp`")

  go polymark poly/over/nolabel/pal=white/line/color=$6%black|black|red|green|blue|lightblue|purple|violet>purple|*>black% xpl,ypl,zpl,rectangle,1.8
  repeat/k=1:20 (let/quiet xp=xpl[j=`k`]; let/quiet yp=ypl[j=`k`]; let/quiet zp=zpl[j=`k`]; IF `xp NE 999` THEN IF `yp ne 999` THEN  label `xp`, `yp-0.8`, 0,0, 0.09, "`zp`")
ENDIF

IF $2"|mark>1|*>0|" THEN
! Mark the extrema with L and H 
! Allow either violet or purple to be specified.   

  GO polymark POLY/OVER/NOLABEL/PAL=$5%black|black|red|green|blue|lightblue|violet|purple>violet|*>black%/fill xph,yph,,hletter,1.2
  GO polymark POLY/OVER/NOLABEL/PAL=$6%black|black|red|green|blue|lightblue|violet|purple>violet|*>black%/fill xpl,ypl,zpl,lletter,1.2
ENDIF

! Restore input region
SET REGION save

SET MODE/LAST VERIFY
