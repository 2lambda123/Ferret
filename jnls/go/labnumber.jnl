\cancel mode verify
! labnumber
! *10/02 *acm* 

! Description: Label locations on a 2-D plot with a number and optional text
!
!   usage:
!       GO labnumber xloc yloc value [size] [location] [LN_prefix] [LN_suffix]
!   where:
!       xloc      horizontal location of the points
!       yloc      vertical location of the points
!       value     function value at the points
!       size      size of the labels (default 0.12 inches)
!       justify   justification of the label: right, left(default), center
!       prefix    text and/or a font setting, to preceed each label
!       suffix    text following each label
!       ndigits   maximum number of digits to the right of the decimal point
!
!
! example:
!       yes? USE coads_climatology
!       yes? SET REGION/L=1/X=100:360/Y=-20:60
!       yes? FILL sst
!       yes? LET/QUIET xpts = {123, 198, 245, 260, 330}
!       yes? LET/QUIET ypts = {-10,  10, -10,  10, -10}
!       yes? LET/QUIET vals = SAMPLEXY(sst, xpts, ypts)
!       yes? GO labnumber.jnl xpts ypts vals 0.14 "left" " " " Deg C" 1
!
!       yes? ! or, to set a color and font, for example,
!       yes? GO labnumber.jnl xpts ypts vals 0.10 "right" "@P5@TR" " C" 0

! check arguments
QUERY/IGNORE $1%<Usage: GO labnumber  xpts ypts var [size] [justify] [prefix] [suffix] [ndigits]%
QUERY/IGNORE $2%<argument 2 is ypts %
QUERY/IGNORE $3%<argument 3 is values%
QUERY/IGNORE $4%0.12%
QUERY/IGNORE $5%-1|right>1|left>-1|centered>0|*>-1|%

DEFINE SYMBOL LN_prefix = "$6%  %"
DEFINE SYMBOL LN_suffix = "$7%  %"
LET/QUIET LN_prefix_exists = $6"0|*>1"
LET/QUIET LN_ndig = $8%-1%

LET/QUIET LN_center = $5%-1|right>1|left>-1|centered>0|*>-1|%
LET/QUIET LN_lsiz = $4%0.2% 
LET/QUIET LN_xpts = $1
LET/QUIET LN_ypts = $2
LET/QUIET LN_vals = $3

! Save region; we need to cancel it to get the right LN_npts
DEFINE REGION/DEFAULT save
CAN REGION/X/Y

LET/QUIET LN_x = XSEQUENCE(LN_xpts)
LET/QUIET LN_y = XSEQUENCE(LN_ypts)
LET/QUIET LN_v = XSEQUENCE(LN_vals)

LET/QUIET LN_x = LN_xpts
LET/QUIET LN_y = LN_ypts
LET/QUIET LN_v = LN_vals

! Initialize these; 
LET/QUIET LN_p = 0
LET/QUIET LN_n = 0

! Case where we arent fixing the formatting of the values

LET/QUIET LN_npts = `LN_x,return=isize`
IF `LN_ndig LT 0` THEN

  REPEAT/i=1:`LN_npts` ( \
    LET/QUIET LN_xval =LN_x[i=`i`];\
    LET/QUIET LN_yval =LN_y[i=`i`];\
    LET/QUIET ln_vval = LN_v[i=`i`];\
    LET/QUIET LN_out1 = STRCAT(($LN_prefix),"`ln_vval`");\
    LET/QUIET LN_outlab = STRCAT(LN_out1,($LN_suffix));\  
    LET/QUIET LN_p = ln_vval AND `LN_prefix_exists eq 1`;\
    LET/QUIET LN_n = ln_vval AND `LN_prefix_exists eq 0`;\
    IF `LN_p NE 0` THEN LABEL `LN_xval`, `LN_yval`, `LN_center`, 0, `LN_lsiz`,  `LN_outlab`;\
    IF `LN_n NE 0` THEN LABEL `LN_xval`, `LN_yval`, `LN_center`, 0, `LN_lsiz`, "`LN_outlab`")

ELSE 

! Adjust format of values to show fewer digits if specified.

  REPEAT/i=1:`LN_npts` ( \
    LET/QUIET LN_xval =LN_x[i=`i`];\  
    LET/QUIET LN_yval =LN_y[i=`i`];\
    LET/QUIET ln_vval = LN_v[i=`i`];\ 
    LET/QUIET LN_lab = "`ln_vval`";\
    LET/QUIET LN_ndot = STRINDEX(LN_lab, ".");\ 
    LET/QUIET LN_nlen = STRLEN(LN_lab);\
    LET/QUIET LN_wh = LN_nlen - LN_ndot + LN_ndig;\  
    LET/QUIET LN_out1 = SUBSTRING(LN_lab, 1, LN_wh);\
    LET/QUIET LN_out2 = STRCAT(($LN_prefix), LN_out1);\
    LET/QUIET LN_outlab = STRCAT(LN_out2, ($LN_suffix));\
    LET/QUIET LN_p = ln_vval AND `LN_prefix_exists eq 1`;\
    LET/QUIET LN_n = ln_vval AND `LN_prefix_exists eq 0`;\
    IF `LN_p NE 0` THEN LABEL `LN_xval`, `LN_yval`, `LN_center`, 0, `LN_lsiz`, `LN_outlab`;\
    IF `LN_n NE 0` THEN LABEL `LN_xval`, `LN_yval`, `LN_center`, 0, `LN_lsiz`, "`LN_outlab`")
ENDIF

!    IF `LN_prefix_exists eq 1` THEN LABEL `LN_xval`, `LN_yval`, `LN_center`, 0, `LN_lsiz`,  `LN_outlab`;\
!    IF `LN_prefix_exists eq 0` THEN LABEL `LN_xval`, `LN_yval`, `LN_center`, 0, `LN_lsiz`, "`LN_outlab`")

! clean up
SET REGION save
CANCEL VARIABLE LN_*
CANCEL SYMBOL LN_*

SET MODE/LAST VERIFY
