\cancel mode verify

! stack_stick.jnl
! stack plot of stick vector plots at each depth
! acm 7/2005

!See example in call_stack_stick.jnl

! go stack_stick uvar vvar [yaxmin] [yaxmax]

! $1 variable name
! $2 lower limit on Y axes
! $3 upper limit on Y axes

QUERY/IGNORE $1%<Usage: GO stack_stick  U V [yaxmin] [yaxmax]

IF `"$2%nothing%" ne "nothing"` THEN
  QUERY/IGNORE $3%<if arg2 yaxmin given then arg3 yaxmax is required
ENDIF

cancel view
! Set up parameters for defining the viewports and making the plots.

let zz = z[gz=$1]

let ks = `zz,return=kstart`
let ke = `zz,return=kend`
let nz = `zz,return=ksize`

if `nz gt 20` then
  message "too many levels: use less than 20" 
  exit/script
endif

let y2 = 0.9
let del = 0.8/`nz`
let y1 = `y2 - del` + 0.02

! This viewport will be used to draw the time axis at the bottom 
! and to label the z axes.

def view/axes/xlim=0.1:0.9/ylim=0.095:0.9 tv

! Define viewports.
!
! The rt_* viewports, one for each depth being plotted are used to plot the right-
! hand axes giving the data range for each Z lavel.

! The lf_* viewports are the same size and shape. They will be used to plot the
! left-hand axis with the corresponding depth

repeat/range=`ks`:`ke`/name=m (def view/axes/xlim=0.1:0.9/ylim=`y1`:`y2` rt_`m`; \
                          def view/axes/xlim=0.1:0.9/ylim=`y1`:`y2` lf_`m`; \
                          let y1 = `y1 - del`; let y2 = `y2 - del`)

! Draw the time axis by plotting a variable having no data in the vertical range.
! Label the vertical stacks of axes in the middle.

set view tv
let/bad=9999 all_zero = if missing($1,0) then 0 else 0*$1
plot/noy/ax=0,1,0,0/nolab/k=`nz`/vlim=100:200 all_zero 

label/nouser `($ppl$xlen)/2`,-0.8, 0, 0, 0.14 "`$1,return=title`"
label/nouser -0.6, `($ppl$ylen)/2`, 0, 90, 0.12 "Depth (`$1,return=zunits`)"
label/nouser `($ppl$xlen)+0.7`, `($ppl$ylen)/2`, 0, 90, 0.12 "@AC`$1,return=units`"

! Draw a plot for each series, labelling on the left with the depth.

IF `"$2%nothing%" eq "nothing"` THEN

! Put most of the labels on , except for the title and the depth

set view lf_`ks`
plot/noy/k=1/axes=0,0,1,0/nolab/color=white/set $1*0
   ppl tics,0,0,0,0
   ppl axlsze,0,0
ppl plot 
let zlab = zz[k=1] 
label/nouser `-1*0.4*($ppl$xorg)`, `($ppl$ylen)/2`, -1, 0, 0.15, "`zlab`" 
ppl tics,.125,.25,.125,.25  
ppl axlint,2,2 
ppl axlsze,0.1,0.1 
ppl axatic 5,5 

set view rt_`ks`
plot/noy/k=1/axes=0,0,0,1/title=" "/set $1 
   ppl axatic ,3; go unlabel 6 
   ppl ylab " "; ppl plot


repeat/range=`ks+1`:`ke`/name=m (set view lf_`m`; \
   plot/noy/k=`m`/axes=0,0,1,0/nolab/color=white/set 0*$1; \
   ppl tics,0,0,0,0; ppl axlsze,0,0; ppl plot; \
   let zlab = zz[k=`m`]; \
   label/nouser `-1*0.4*($ppl$xorg)`, `($ppl$ylen)/2`, -1, 0, 0.15, "`zlab`"; \
   ppl tics,.125,.25,.125,.25; \ 
   ppl axlint,2,2; \
   ppl axlsze,0.1,0.1; \
   ppl axatic 5,5; \
   ;\
   set view rt_`m`; \
   plot/noy/k=`m`/axes=0,0,0,1/nolab/set $1; \
   ppl axatic ,3; ppl axlabp ,1; ppl plot; \
)


ELSE

! Put most of the labels on , except for the title and the depth

set view lf_`ks`
plot/noy/k=1/axes=0,0,1,0/nolab/color=white/set 0*$1 
  ppl tics,0,0,0,0 
  ppl axlsze,0,0
ppl plot 
let zlab = zz[k=1] 
label/nouser `-1*0.4*($ppl$xorg)`, `($ppl$ylen)/2`, -1, 0, 0.15, "`zlab`" 
ppl tics,.125,.25,.125,.25  
ppl axlint,2,2 
ppl axlsze,0.1,0.1 
ppl axatic 5,5 

set view rt_`ks`
plot/noy/k=1/axes=0,0,0,1/title=" "/set $1 
   ppl yaxis $2 $3 `($3-$2)/4` 
   go unlabel 6 
   ppl ylab " " 
ppl plot


repeat/range=`ks+1`:`ke`/name=m (set view lf_`m`; \
   plot/noy/k=`m`/axes=0,0,1,0/nolab/color=white/set 0*$1; \
   ppl tics,0,0,0,0; ppl axlsze,0,0; ppl plot; \
   let zlab = zz[k=`m`]; \
   label/nouser `-1*0.4*($ppl$xorg)`, `($ppl$ylen)/2`, -1, 0, 0.15, "`zlab`"; \
   ppl tics,.125,.25,.125,.25; \ 
   ppl axlint,2,2; \
   ppl axlsze,0.1,0.1; \
   ppl axatic 5,5; \
   ; \
   set view rt_`m`; \
   plot/noy/k=`m`/axes=0,0,0,1/vlimits=$2:$3/nolab/set $1; \
   ppl yaxis $2 $3 `($3-$2)/4` ; ppl axlabp ,1; ppl plot; \
)

ENDIF

set mode/last verify
