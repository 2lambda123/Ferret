\!cancel mode verify
! view_palette.jnl
! Description: make a color bar illustrating a palette

! Note, the patelle file must be in the current directory

!   usage:    GO view_palette palette_name type skip
!   example:
!            yes? set window/asp=.3/size=.1
!            yes? go view_palette up_rainbow_discrete INDEX 3

! check args
QUERY/IGNORE $1">You didnt supply a palette name"
QUERY/IGNORE $2"PERCENT|PERCENT|VALUE|INDEX|"

! open palette file
file/var=p/skip=$3"0" $1.spk

! number of points in the palette
let n = p[x=@ngd]
define symbol N `n`
define symbol Nm1 `n-1`

! re-open using its own column one axis as a grid
define axis/x pax = p[i=1:($N)]
define grid/x=pax gg
cancel data 1
file/var=p/skip=$3"0"/grid=gg $1.spk


! set up PLOT+ levels corresponding to the percentages (or values or index)
ppl lev ()
IF `($N) GT 1` THEN
  ppl lev (`p[i=1]-(p[i=2]-p[i=1])/2`)
  repeat/i=1:`($N)-1` ppl lev (`(p+p[i=@shf:+1])/2`)
  ppl lev (`p[i=($N)]+(p[i=($N)]-p[i=($Nm1)])/2`)
ELSE
  ppl lev (`p`)
ENDIF

! make the palette specimen
go bold
IF $2"1|PERCENT>1|*>0" THEN
  shade/palette=$1/lev/nolab/nokey/set_up/xlimits=0:100:10 p+0*j[j=1:2]
  ppl axlint 1
ELIF $2"1|INDEX>1|*>0" THEN
  shade/palette=$1/lev/nolab/nokey/set_up/xlimits=1:($N):1 p+0*j[j=1:2]
  ppl axlint 1
ELSE
  shade/palette=$1/lev/nolab/nokey/set_up p+0*j[j=1:2]
ENDIF
IF $2"1|INDEX>1|*>0" THEN
  ppl xfor (I)
ENDIF
ppl xlab $2"PERCENT"
ppl ylab
ppl axlsze 0.3
ppl labset 0.16, 0.3
ppl axset 0,1,0,0
ppl origin 1.4,1.6
ppl shade

! clean up
!cancel data/all
!cancel variable/all
!ppl axset 1,1,1,1

set mode/last verify