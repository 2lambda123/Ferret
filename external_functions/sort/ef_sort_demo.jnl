set MODE VERIFY
!    
!    
!    FERRET external functions version 1.1    Use ferret_c
 
! ******************************************************************** 
MESSAGE



! Description: examples of the sorting external functions for FERRET.
 
! ef_sort_demo.jnl *acm* 9/98 - sort EF demo
CANCEL REGION
SET MODE VERIFY     ! display the command lines below at the user's console
 
!         This demonstration/tutorial will introduce the FERRET
!         external functions for sorting data.
 
! ******************************************************************** 
MESSAGE

!         A first simple example of sorting: Sort a list of data.

USE coads_climatology
SET REGION/x=19e/y=77n/z=0/l=1:6
LIST sst

!         The sequence of calls is:  Compute the sorted indices, then sample 
!         the data using the new order of indices.  This is COADS sea surface 
!         data at a location where some of the data is missing.
! ******************************************************************** 
MESSAGE

!       Show the indices of the sorted list.  

LET tsorted_indices = sortl(sst[l=1:6])
LIST tsorted_indices

!       Show the sorted data:

LET tsorted_sst = samplel(tsorted_indices, sst[l=1:6])
LIST tsorted_sst
! ******************************************************************** 
MESSAGE

!         A more involved example:  What is the Sea Surface 
!         Temperature here 3 months after the strongest winds 
!         there?  Use the COADS monthly climatology data set.

CAN REGION 
SET DATA coads_climatology

!         Choose a region in the Pacific, and define the westerly_wind 
!         variable to be the monthly west winds averaged over this region.
!         Sort the westerly winds, lowest to highest. "sorted_indices3" 
!         is the indices 3 months after.

LET westerly_wind = uwnd[x=160e:180@ave,y=5s:5n@ave]
LET sst_e = sst[x=180:80w@ave,y=5s:5n@ave]

LET sorted_indices = sortl(westerly_wind)
LET sorted_indices3 = MOD((sorted_indices + 2), 12) + 1

!         We will plot sorted winds vs the SST's, 3 months later, 
!         and in another region.   First sample the westerly winds 
!         by their sorted indices.  Then order the SST's according  
!         to the sorted wind indices plus 3 months.  

LET wwe_by_wwe = samplel(sorted_indices, westerly_wind)
LET sst_by_wwe = samplel(sorted_indices3, sst_e)

!         The SST's of interest are to the east of our westerly_wind 
!         region.  Get the number of valid data in the sort. 

LET leng = wwe_by_wwe[l=@NGD]
! ******************************************************************** 
MESSAGE

!          Make scatter plots: sampleed winds vs SST, and mark the
!          highest winds with a symbol.  The computation takes a moment.

PLOT/VS/TITLE="SST 3 months after High Westerly Winds"/SET_UP/l=1:`leng` wwe_by_wwe, sst_by_wwe
ppl xaxis -3.3, -1.9, 0.1
ppl yaxis, 25.4, 27.4, 0.2
ppl xlab "Sorted Westerly Winds from 160E to 180"
ppl ylab "SST from 180 to 80W 3 months after Westerly winds"
ppl plot

PLOT/VS/OVER/SYMBOL=18/NOLABELS/l=`leng-3`:`leng` wwe_by_wwe, sst_by_wwe

!         done  
