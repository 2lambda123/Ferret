set MODE VERIFY
!    
!    
!    FERRET external functions version 1.1  Use ferret_c
 
! ******************************************************************** 
MESSAGE



use coads_climatology
set region/x=140/y=40/z=0/l=1:12

list sst
let tsorted_indices = sortl(sst)
let tsorted_sst = samplel(tsorted_indices, sst)

list tsorted_indices
list tsorted_sst


! Jon's test case.  
! What is the temp here three months after the strongest winds there?
!  Scatter plots of all data, then highest winds.

can region 
set data coads

let westerly_wind = uwnd[x=160e:180@ave,y=5s:5n@ave]
let sorted_indices = sortl(westerly_wind)
let sorted_indices3 = MOD((sorted_indices+2), 648) + 1

let wwe_by_wwe = samplel(sorted_indices, westerly_wind)
let sst_by_wwe = samplel(sorted_indices3, sst)

let el_nino = sst_by_wwe[x=180:80w@ave,y=5s:5n@ave]
let leng = wwe_by_wwe[l=@NGD]

!  Scatter plots of all data, then highest winds.

plot/vs wwe_by_wwe, el_nino
plot/vs/over/symbol=18/l=`leng-20`:`leng` wwe_by_wwe, el_nino

!  Line plots of the sorted data.
plot wwe_by_wwe  
plot/over el_nino - 24

! line plots of the data from highest winds.

plot/l=`leng-20`:`leng` wwe_by_wwe 
plot/over/l=`leng-20`:`leng` el_nino - 24


!  See how components of wind correlate

can region 
set data coads

let westerly_wind = uwnd[x=160e:180@ave,y=5s:5n@ave]
let wsorted_indices = sortl(westerly_wind)
let ww_by_ww = samplel(wsorted_indices, westerly_wind)

let speed_wind = wspd[x=160e:180@ave,y=5s:5n@ave]
let sp_by_ww = samplel(wsorted_indices, speed_wind)


let len_w = ww_by_ww[l=@NGD]
list len_w

plot ww_by_ww 
plot/over sp_by_ww 

plot/vs ww_by_ww, sp_by_ww
plot/vs/over/symbol=18/l=`len_w-20`:`len_w` ww_by_ww, sp_by_ww



!  See how air and sea temp correlate

can region 
set data coads

let sea_temp = sst[x=160e:180@ave,y=5s:5n@ave]
let stsorted_indices = sortl(sea_temp)
let st_by_st = samplel(stsorted_indices, sea_temp)

let air_temp = airt[x=160e:180@ave,y=5s:5n@ave]
let at_by_st = samplel(stsorted_indices, air_temp)


let len_s = st_by_st[l=@NGD]
list len_s

plot st_by_st 
plot/over at_by_st 

plot/vs/xlimits=26:32:1/ylimits=26:32:1 st_by_st, at_by_st

!  Test sortx and sample by sorting elevation data.

can region
use etopo120
let xsorted_indices = sorti(rose)
let xsorted_elev = samplei(xsorted_indices, rose)
shade xsorted_elev

let xsorted_indices = sorti(rose) + 240
shade xsorted_elev

! test sortx with data containing bad/missing data.

can region
set data coads
set region/l=500
shade sst

let xsorted_indices = sorti(sst)
let xsorted_sst = samplei(xsorted_indices, sst)
shade xsorted_sst

set region/l=500/y=0
let len = xsorted_sst[i=@ngd]
list len


! test sorty with data containing bad/missing data.

can region
set data coads
set region/l=500
shade sst

let ysorted_indices = sortj(sst)
let ysorted_sst = samplej(ysorted_indices, sst)
shade ysorted_sst

set region/l=500/x=180
let len = ysorted_sst[j=@ngd]
list len



! test sortz
 
can region
set data ocean_atlas_annual
show data ocean_atlas_annual

set region/y=0
shade temp

let zsorted_indices = sortk(temp)
let zsorted_temp = samplek(zsorted_indices, temp)
shade zsorted_temp

set region/y=0/x=110w
let len = zsorted_temp[k=@ngd]
list len



! test sortt with data containing bad/missing data.

can region 
set data coads
set region/y=0
! shade sst

let tsorted_indices = sortl(sst)
let tsorted_sst = samplel(tsorted_indices, sst)
! shade tsorted_sst

set region/x=110w/y=0
plot sst
plot tsorted_sst
let len = tsorted_sst[l=@ngd]
list len

list/l=1:`len` tsorted_sst
list/l=`len-20`:`len` tsorted_sst
